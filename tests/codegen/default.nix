{ lib
, linkerScript
, rvv-codegen
, makeBuilder
  # Instead of testing feature is supported on TOP level,
  # codegen case are always generated with supported code.
, currentFeatures
}:

let
  builder = makeBuilder { casePrefix = "codegen"; };
  makeCaseName = lib.replaceStrings [ "." ] [ "_" ];
  extraValueFromFeatures = pattern:
    lib.last
      (lib.splitString ":"
        (lib.head
          (lib.filter
            (lib.hasPrefix pattern)
            currentFeatures)));
  vlen = extraValueFromFeatures "vlen";
  xlen = extraValueFromFeatures "xlen";

  build = { rawCaseName, extra }:
    builder
      rec {
        caseName = makeCaseName rawCaseName;

        includeArgs = [
          "-I${./override_include}"
          "-I${rvv-codegen}/include"
        ];

        dontUnpack = true;

        buildPhase = ''
          runHook preBuild

          ${rvv-codegen}/bin/single \
            -VLEN "${vlen}" \
            -XLEN "${xlen}" \
            -repeat 16 \
            -testfloat3level 2 \
            -configfile ${rvv-codegen}/configs/${rawCaseName}.toml \
            -outputfile $pname.S

          # temporary fix, to be extended later
          if $CC $pname.S -T ${linkerScript} $includeArgs -o $pname.elf ; then
            echo "link with 4M SRAM succeded"
          else
            echo "link with 4M SRAM failed, use DDR instead"
            sed 's/>SRAM/>DDR/' ${linkerScript} > t1-ddr.ld
            $CC $pname.S -T t1-ddr.ld $includeArgs -o $pname.elf
          fi

          runHook postBuild
        '';

        meta.description = "test case '${caseName}' generated by codegen";
      } // extra;

  buildTestsFromFile = file: extra:
    with lib;
    let
      rawCaseNames = lib.splitString "\n" (lib.fileContents file);
    in
    (listToAttrs
      (map
        (rawCaseName: nameValuePair
          (makeCaseName rawCaseName)
          (build { inherit rawCaseName extra; })
        )
        rawCaseNames));

  commonTests = buildTestsFromFile ./common.txt { featuresRequired = [ ]; };
  fpTests = buildTestsFromFile ./fp.txt { featuresRequired = [ "zve32f" ]; };
  zvbbTests = buildTestsFromFile ./zvbb.txt { featuresRequired = [ "zvbb" ]; };
in
lib.recurseIntoAttrs (
  commonTests //
  lib.optionalAttrs (lib.elem "zve32f" currentFeatures) fpTests //
  lib.optionalAttrs (lib.elem "zvbb" currentFeatures) zvbbTests
)

