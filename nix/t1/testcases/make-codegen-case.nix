{ lib, jq, stdenv, rvv-codegen }:

{ caseName, xLen ? 32, vLen ? 1024, fp ? false, ... }@inputs:
let
  caseNameEscaped = builtins.replaceStrings ["."] ["_"] caseName;
in
stdenv.mkDerivation (rec {
  name = "codegen.${caseNameEscaped}";

  dontUnpack = true;

  nativeBuildInputs = [ jq rvv-codegen ];

  NIX_CFLAGS_COMPILE = [
    "-mabi=ilp32f"
    "-march=rv32gcv"
    "-mno-relax"
    "-static"
    "-mcmodel=medany"
    "-fvisibility=hidden"
    "-nostdlib"
    "-fno-PIC"
    "-I${rvv-codegen}/include"
  ];

  buildPhase = ''
    runHook preBuild

    single \
      -VLEN ${toString vLen} \
      -XLEN ${toString xLen} \
      -testfloat3level 1 \
      -configfile ${rvv-codegen}/configs/${caseName}.toml \
      -outputfile ${caseNameEscaped}.S

    ${stdenv.targetPlatform.config}-cc ${caseNameEscaped}.S -o ${name}.elf

    runHook postBuild
  '';

  installPhase = ''
    runHook preInstall

    mkdir -p $out/bin
    cp ${name}.elf $out/bin

    jq --null-input \
      --arg name ${caseNameEscaped} \
      --arg type intrinsic \
      --argjson xLen ${toString xLen} \
      --argjson vLen ${toString vLen} \
      --argjson fp ${lib.boolToString fp} \
      --arg elfPath "$out/bin/${name}.elf" \
      '{ "name": "${name}", "type": $type, "xLen": $xLen, "vLen": $vLen, "fp": $fp, "elf": { "path": $elfPath } }' \
      > $out/${name}.json

    runHook postInstall
  '';

  meta.description = "Test case '${caseNameEscaped}', generated by [rvv-codegen](https://github.com/ksco/riscv-vector-tests).";

  dontFixup = true;
} // inputs)
